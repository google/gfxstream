load("@protobuf//bazel:cc_proto_library.bzl", "cc_proto_library")
load("@protobuf//bazel:proto_library.bzl", "proto_library")
load("@rules_cc//cc:defs.bzl", "cc_library", "cc_shared_library")

package(
    default_applicable_licenses = ["//:gfxstream_license"],
    default_visibility = ["//:gfxstream"],
)

proto_library(
    name = "gfxstream_guest_glesv2encoder_proto",
    srcs = [
        "ProgramBinary.proto",
    ],
)

cc_proto_library(
    name = "gfxstream_guest_glesv2encoder_cc_proto",
    deps = [":gfxstream_guest_glesv2encoder_proto"],
)

cc_library(
    name = "gfxstream_guest_glesv2_entries",
    textual_hdrs = [
        "gl2_entry.cpp",
    ],
    visibility = [
        "//guest/GLESv2:__pkg__",
    ],
)

cc_library(
    name = "gfxstream_guest_glesv2encoder",
    srcs = [
        "GL2Encoder.cpp",
        "GL2EncoderUtils.cpp",
        "GLESv2Validation.cpp",
        "IOStream2.cpp",
        "gl2_client_context.cpp",
        "gl2_enc.cpp",
        "gl2_entry.cpp",
    ],
    hdrs = glob([
        "**/*.h",
    ]),
    includes = [
        ".",
    ],
    linkopts = [
        # Search the current directory for this library's shared libs.
        # Used for the end2end tests which package all of the guest
        # libraries into a single `testdata` directory for testing.
        "-Wl,-rpath,'$$ORIGIN'",
    ],
    deps = [
        ":gfxstream_guest_glesv2encoder_cc_proto",
        "//common/logging:gfxstream_common_logging",
        "//guest/OpenglCodecCommon:gfxstream_guest_openglcodeccommon",
        "//guest/android-emu:gfxstream_guest_base",
        "//third_party/opengl:gfxstream_opengl_headers",
        "@mesa//:mesa_gfxstream_guest_iostream",
    ],
)

cc_shared_library(
    name = "gfxstream_guest_glesv2encoder_shared",
    dynamic_deps = [
        "//guest/OpenglCodecCommon:gfxstream_guest_openglcodeccommon_shared",
    ],
    shared_lib_name = "libGLESv2_enc.so",
    deps = [
        ":gfxstream_guest_glesv2encoder",
    ],
)
